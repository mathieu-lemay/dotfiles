prompt_basic_setup() {
    printc() {
        if [ $# -gt 1 ]; then
            c="%F{${1}}"
            shift
        else
            c="%f"
        fi

        echo -n "${c}$1"
    }

    prompt_venv() {
        # Show the prompt name if defined, otherwise, fallback to the basename of the venv's path
        [[ -z "${VIRTUAL_ENV:-}" ]] && return

        local venvname

        # Use VIRTUAL_ENV_PROMPT with trimmed leading/trailing spaces if available
        venvname="${VIRTUAL_ENV_PROMPT:+${${VIRTUAL_ENV_PROMPT##[[:space:]]##}%%[[:space:]]##}}"
        # Fallback on the basename otherwise
        venvname="${venvname:-${VIRTUAL_ENV##*/}}"

        printc ${cyan} "[${venvname}] "
    }

    prompt_status() {
        local -a _status
        [[ $RETVAL -ne 0 ]] && _status+="(!) "

        [[ -n "${_status}" ]] && printc ${red} "${_status}"
    }

    prompt_host() {
        if [[ -n "${SSH_CLIENT:-}" ]]; then
            fg="${yellow}"
        else
            fg="%(!.${red}.${green})"
        fi

        printc ${fg} "${ZSH_PROMPT_USERNAME:-%n}@${ZSH_PROMPT_HOSTNAME:-%m}"
    }

    prompt_dir() {
        printc ":"
        printc ${blue} "%1~"
    }

    prompt_end() {
        printc "$ "
    }

    build_ps1() {
        RETVAL=$?

        prompt_status
        prompt_host
        prompt_dir
        prompt_end
    }

    PS1='$(build_ps1)'
    PS2='> '
    RPROMPT=''
}

# vi: ft=zsh
